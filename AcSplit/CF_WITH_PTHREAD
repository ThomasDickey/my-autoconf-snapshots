dnl CF_WITH_PTHREAD version: 1 updated: 2008/03/29 13:42:21
dnl ---------------
dnl Check for POSIX thread library.
AC_DEFUN([CF_WITH_PTHREAD],
[
AC_MSG_CHECKING(if you want to link with the pthread library)
AC_ARG_WITH(pthread,
    [  --with-pthread          use POSIX thread library],
    [with_pthread=$withval],
    [with_pthread=no])
AC_MSG_RESULT($with_pthread)

if test "$with_pthread" != no ; then
    AC_CHECK_HEADER(pthread.h,[
        AC_DEFINE(HAVE_PTHREADS_H)
        AC_CHECK_LIB(pthread,pthread_create,[
            LIBS="-lpthread $LIBS"
            AC_DEFINE(HAVE_LIBPTHREADS)
            with_pthread=yes
        ],[
            AC_MSG_ERROR(Cannot link with pthread library)
        ])
    ])
fi
])

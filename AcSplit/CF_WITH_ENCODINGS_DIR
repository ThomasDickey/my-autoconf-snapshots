dnl CF_WITH_ENCODINGS_DIR version: 7 updated: 2023/11/22 19:48:06
dnl ---------------------
dnl Configure option to specify the location of encodings.dir, for programs
dnl that must read it directly.
AC_DEFUN([CF_WITH_ENCODINGS_DIR],
[
AC_MSG_CHECKING(for location of encodings.dir file)
AC_ARG_WITH(encodings-dir,
[[  --with-encodings-dir[=XXX] file used for encodings directory (default: auto)]],
[ENCODINGS_DIR_FILE=$withval],
[ENCODINGS_DIR_FILE=auto])

CF_VERBOSE()
case "x$ENCODINGS_DIR_FILE" in
(xauto|xyes|xno)
	ENCODINGS_DIR_FILE=unknown
	for cf_leaf in lib share
	do
		for cf_path in \
			/usr/X11R7/$cf_leaf/X11/fonts \
			/usr/X11R6/$cf_leaf/X11/fonts \
			/usr/local/$cf_leaf/X11/fonts \
			/opt/local/$cf_leaf/fonts/X11 \
			/opt/X11/$cf_leaf/X11/fonts \
			/usr/$cf_leaf/fonts/X11 \
			/usr/$cf_leaf/X11/fonts
		do
			test -d "$cf_path" || continue
			cf_encodings_dir=$cf_path/encodings/encodings.dir
			CF_VERBOSE(...testing $cf_encodings_dir)
			if test -f $cf_encodings_dir ; then
				ENCODINGS_DIR_FILE=$cf_encodings_dir
				break
			fi
		done
	done
	;;
(*)
	if test ! -f "$ENCODINGS_DIR_FILE" ; then
		AC_MSG_WARN(Encodings file not found: $ENCODINGS_DIR_FILE)
		ENCODINGS_DIR_FILE=/usr/share/fonts/X11
	fi
	;;
esac
AC_MSG_RESULT($ENCODINGS_DIR_FILE)
test $ENCODINGS_DIR_FILE = unknown && ENCODINGS_DIR_FILE=
AC_SUBST(ENCODINGS_DIR_FILE)
AC_DEFINE_UNQUOTED(ENCODINGS_DIR_FILE, "$ENCODINGS_DIR_FILE", [Define to pathname of file used for encodings directory])
])dnl

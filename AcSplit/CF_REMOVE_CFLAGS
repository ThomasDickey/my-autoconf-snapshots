dnl CF_REMOVE_CFLAGS version: 1 updated: 2021/08/28 15:20:37
dnl ----------------
dnl Remove a given option from CFLAGS/CPPFLAGS
dnl $1 = option to remove
dnl $2 = variable to update
dnl $3 = nonempty to allow verbose message
define([CF_REMOVE_CFLAGS],
[
cf_tmp_cflag=`echo "x$1" | sed -e 's/^.//' -e 's/=.*//'`
cf_old_cflag="[$]$2"

case "[$]$2" in
(*$1=*)
	cf_old_cflag=`echo "x$cf_old_cflag" | sed -e 's/^.//' -e "s%$cf_tmp_cflag=[[^ 	]]*%%g"`
	;;
(*$1\ *)
	cf_old_cflag=`echo "x$cf_old_cflag" | sed -e 's/^.//' -e "s%${cf_tmp_cflag}.%%"`
	;;
(*$1)
	cf_old_cflag=`echo "x$cf_old_cflag" | sed -e 's/^.//' -e "s%$cf_tmp_cflag%%"`
	;;
esac

if test "[$]$2" != "$cf_old_cflag" ;
then
	ifelse([$3],,,[CF_VERBOSE(removing old option $1 from $2)])
	$2="$cf_new_cflag"
fi
])dnl

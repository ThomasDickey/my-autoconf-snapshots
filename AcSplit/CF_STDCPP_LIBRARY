dnl CF_STDCPP_LIBRARY version: 10 updated: 2015/09/12 15:53:39
dnl -----------------
dnl Check for -lstdc++, which is GNU's standard C++ library.
dnl If $CXXLIBS is set, add that to the libraries used for test-linking.
dnl If -lstdc++ was not already added to $LIBS or $CXXLIBS, and is needed,
dnl add it to $CXXLIBS.
AC_DEFUN([CF_STDCPP_LIBRARY],
[
if test -n "$GXX" ; then

	cf_save="$LIBS"
	LIBS="$LIBS $CXXLIBS"
	AC_MSG_CHECKING(if we already have C++ library)
	AC_TRY_LINK([
			#include <iostream>],[
			std::cout << "Hello World!" << std::endl;],
		[cf_have_libstdcpp=yes],
		[cf_have_libstdcpp=no])
	AC_MSG_RESULT($cf_have_libstdcpp)
	LIBS="$cf_save"

	if test $cf_have_libstdcpp != yes
	then
		case $cf_cv_system_name in
		(os2*)
			cf_stdcpp_libname=stdcpp
			;;
		(*)
			cf_stdcpp_libname=stdc++
			;;
		esac

		AC_CACHE_CHECK(for library $cf_stdcpp_libname,cf_cv_libstdcpp,[
			cf_save="$LIBS"
			LIBS="$LIBS $CXXLIBS"
			CF_ADD_LIB($cf_stdcpp_libname)
		AC_TRY_LINK([
				#include <iostream>],[
				std::cout << "Hello World!" << std::endl;],
			[cf_cv_libstdcpp=yes],
			[cf_cv_libstdcpp=no])
			LIBS="$cf_save"
		])
		test "$cf_cv_libstdcpp" = yes && { CF_ADD_LIB($cf_stdcpp_libname,CXXLIBS) }
	fi
fi
])dnl

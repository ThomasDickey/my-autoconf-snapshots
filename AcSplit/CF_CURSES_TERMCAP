dnl Check if we should include <curses.h> to pick up prototypes for termcap
dnl functions.  On terminfo systems, these are normally declared in <curses.h>,
dnl but may be in <term.h>.  We check for termcap.h as an alternate, but it
dnl isn't standard (usually associated with GNU termcap).
dnl
dnl The 'tgoto()' function is declared in both terminfo and termcap.
AC_DEFUN([CF_CURSES_TERMCAP],
[
AC_REQUIRE([CF_CURSES_TERM_H])
AC_MSG_CHECKING(if we should include curses.h or termcap.h)
AC_CACHE_VAL(cf_cv_need_curses_h,[
cf_save_CFLAGS="$CFLAGS"

for cf_t_opts in "" "NEED_TERMCAP_H"
do
for cf_c_opts in "" "NEED_CURSES_H"
do
    CFLAGS="$cf_save_CFLAGS"
    test -n "$cf_c_opts" && CFLAGS="$CFLAGS -D$cf_c_opts"
    test -n "$cf_t_opts" && CFLAGS="$CFLAGS -D$cf_t_opts"
    AC_TRY_COMPILE([
#if NEED_CURSES_H
# if HAVE_NCURSES_H
#  include <ncurses.h>
# else
#  include <curses.h>
# endif
#endif
#if HAVE_TERM_H
# include <term.h>
#endif
#if NEED_TERMCAP_H
#include <termcap.h>
#endif
],[char *x = tgoto("", 0, 0)],
	[cf_cv_need_curses_h=yes],
	[cf_cv_need_curses_h=no])

	CFLAGS="$cf_save_CFLAGS"
	test "$cf_cv_need_curses_h" = yes && break
done
	test "$cf_cv_need_curses_h" = yes && break
done
if test "$cf_cv_need_curses_h" = yes ; then
	echo "Curses/termcap test succeeded (C:$cf_c_opts, T:$cf_t_opts)" >&AC_FD_CC
	if test -z "$cf_c_opts" ; then
		cf_cv_need_curses_h=termcap.h
	elif test -z "$cf_t_opts" ; then
		cf_cv_need_curses_h=curses.h
	else
		cf_cv_need_curses_h=both
	fi
fi
])
AC_MSG_RESULT($cf_cv_need_curses_h)

case $cf_cv_need_curses_h in
both) #(vi
	AC_DEFINE_UNQUOTED(NEED_CURSES_H)
	AC_DEFINE_UNQUOTED(NEED_TERMCAP_H)
	;;
curses.h) #(vi
	AC_DEFINE_UNQUOTED(NEED_CURSES_H)
	;;
termcap.h) #(vi
	AC_DEFINE_UNQUOTED(NEED_TERMCAP_H)
	;;
esac

])dnl

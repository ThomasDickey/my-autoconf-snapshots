dnl Check for UTMP/UTMPX headers
AC_DEFUN([CF_UTMP],
[
AC_CACHE_CHECK(for utmp implementation,cf_cv_have_utmp,[
	AC_TRY_COMPILE([
#include <sys/types.h>
#include <utmp.h>],
	[struct utmp x],
	[cf_cv_have_utmp=utmp],
	[AC_TRY_COMPILE([
#include <sys/types.h>
#include <utmpx.h>],
		[struct utmpx x],
		[cf_cv_have_utmp=utmpx],
		[cf_cv_have_utmp=no])
		])
	])

if test $cf_cv_have_utmp != no ; then
	AC_DEFINE(HAVE_UTMP)
	test $cf_cv_have_utmp = utmpx && AC_DEFINE(UTMPX_FOR_UTMP)
	CF_UTMP_UT_HOST
	CF_SYSV_UTMP
fi
])

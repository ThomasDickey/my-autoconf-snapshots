dnl CF_WITH_ICON_THEME version: 2 updated: 2012/06/20 05:50:37
dnl ------------------
dnl If asked, check for prerequisites and setup symbols to permit installing
dnl one or more application icons in the Red Hat icon-theme directory
dnl hierarchy. 
dnl
dnl If the prerequisites are missing, give a warning and revert to the long-
dnl standing pixmaps directory.
dnl
dnl Parameters:
dnl
dnl $1 = application icon.  This can be a list, and is not optional.
dnl $2 = default theme (defaults to hicolor)
dnl $3 = formats (defaults to list [.svg .png .xpm])
dnl
dnl Result:
dnl ICON_LIST = reprocessed $1
dnl ICON_THEME = reprocessed $2
dnl ICON_FORMAT = reprocessed $3
AC_DEFUN([CF_WITH_ICON_THEME],
[
ifelse([$1],,[
	AC_MSG_ERROR([macro [CF_WITH_ICON_THEME] requires application-icon name])
],[

AC_MSG_CHECKING(if icon theme should be used)
AC_ARG_WITH(icon-theme,
	[  --with-icon-theme=XXX   install icons into desktop theme (hicolor)],
	[ICON_THEME=$withval],
	[ICON_THEME=no])

case "x$ICON_THEME" in #(vi
xno) #(vi
	;;
x|xyes)
	ICON_THEME=ifelse([$2],,hicolor,$2)
	;;
esac
AC_MSG_RESULT($ICON_THEME)

if test "x$ICON_THEME" = xno
then

	CF_WITH_PIXMAPDIR
	no_icondir="#"

else

	CF_WITH_ICONDIR
	no_pixmapdir="#"

	if test "x$ICONDIR" = xno
	then
		AC_MSG_ERROR(icondir must be set for icon theme)
	fi

	: ${ICON_FORMAT:=ifelse([$3],,[.svg .png .xpm],[$3])}

	AC_MSG_CHECKING([for icon(s) to install])
	for cf_name in $1
	do
		for cf_suffix in $ICON_FORMAT
		do
			cf_icon="${cf_name}${cf_suffix}"
			cf_left=`echo "$cf_icon" | sed -e 's/:.*//'`
			if test ! -f "${cf_left}"
			then
				if test "x$srcdir" != "x."
				then
					cf_icon="${srcdir}/${cf_left}${cf_suffix}"
					cf_left=`echo "$cf_icon" | sed -e 's/:.*//'`
					if test ! -f "${cf_left}"
					then
						continue
					fi
				else
					continue
				fi
			fi
			cf_base=`basename $cf_left`
			case "x${cf_icon}" in #(vi
			*:*) #(vi
				# user-defined mapping
				;;
			*_[[0-9]][[0-9]]*x[[0-9]][[0-9]]*.*) #(vi
				cf_size=`echo "$cf_left"|sed -e 's/^.*_\([[0-9]][[0-9]]*x[[0-9]][[0-9]]*\)\..*$/\1/'`
				cf_left=`echo "$cf_left"|sed -e 's/^\(.*\)_\([[0-9]][[0-9]]*x[[0-9]][[0-9]]*\)\(\..*\)$/\1\3/'`
				cf_icon="${cf_icon}:$cf_size/apps/$cf_base"
				;;
			*.png) #(vi
				cf_size=`file "$cf_left"|sed -e 's/^[[^:]]*://' -e 's/^.*[[^0-9]]\([[0-9]][[0-9]]* x [[0-9]][[0-9]]*\)[[^0-9]].*$/\1/' -e 's/ //g'`
				if test -z "$cf_size"
				then
					AC_MSG_WARN(cannot determine size of $cf_left)
					continue
				fi
				cf_icon="${cf_icon}:$cf_size/apps/$cf_base"
				;;
			*.svg) #(vi
				cf_icon="${cf_icon}:scalable/apps/`basename $cf_icon`"
				;;
			*.xpm)
				AC_MSG_WARN(ignored XPM file in icon theme)
				continue
				;;
			esac
			ICON_LIST="$ICON_LIST ${cf_icon}"
		done
	done
	AC_MSG_RESULT($ICON_LIST)
	if test -z "$ICON_LIST"
	then
		AC_MSG_ERROR(no icons found)
	fi
fi
])

AC_SUBST(ICON_FORMAT)
AC_SUBST(ICON_THEME)
AC_SUBST(ICON_LIST)
])dnl

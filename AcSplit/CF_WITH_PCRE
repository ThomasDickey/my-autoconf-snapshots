dnl CF_WITH_PCRE version: 4 updated: 2010/05/25 19:42:59
dnl ------------
dnl Add PCRE (Perl-compatible regular expressions) to the build if it is
dnl available and the user requests it.  Assume the application will otherwise
dnl use the POSIX interface.
dnl
dnl TODO allow $withval to specify package location
AC_DEFUN([CF_WITH_PCRE],
[
AC_REQUIRE([CF_PKG_CONFIG])

AC_MSG_CHECKING(if you want to use PCRE for regular-expressions)
AC_ARG_WITH(pcre,
	[  --with-pcre             use PCRE for regular-expressions])
test -z "$with_pcre" && with_pcre=no
AC_MSG_RESULT($with_pcre)

if test "$with_pcre" != no ; then
	if test "$PKG_CONFIG" != none && "$PKG_CONFIG" --exists libpcre; then
		cf_libpcre_incs="`$PKG_CONFIG --cflags 2>/dev/null`"
		cf_libpcre_libs="`$PKG_CONFIG --libs 2>/dev/null`"
		CF_ADD_CFLAGS($cf_libpcre_incs)
		LIBS="$cf_libpcre_libs $LIBS"
		AC_CHECK_HEADER(pcreposix.h,[
			AC_DEFINE(HAVE_LIB_PCRE)
			AC_DEFINE(HAVE_PCREPOSIX_H)],[
			AC_MSG_ERROR(Cannot find PCRE POSIX header)])
	else
		AC_CHECK_LIB(pcre,pcre_compile,
			[AC_CHECK_HEADER(pcreposix.h,
				[AC_CHECK_LIB(pcreposix,pcre_compile,
					[AC_DEFINE(HAVE_LIB_PCRE)
					 AC_DEFINE(HAVE_PCREPOSIX_H)
					 LIBS="-lpcreposix -lpcre $LIBS"],
					AC_MSG_ERROR(Cannot find PCRE POSIX library),
					"-lpcre")],
				AC_MSG_ERROR(Cannot find PCRE POSIX header))],
			AC_MSG_ERROR(Cannot find PCRE library))
	fi
fi
])dnl

dnl CF_WITH_PERL version: 2 updated: 2008/08/11 10:36:36
dnl ------------
dnl Check if perl-extension (using embedded perl interpreter) is wanted, and
dnl update symbols.
AC_DEFUN([CF_WITH_PERL],[
AC_MSG_CHECKING(if you want to use perl as an extension language)
AC_ARG_WITH(perl,
	[  --with-perl             enable use of Perl as an extension language],
	[with_perl="$withval"],
	[with_perl=no])
AC_MSG_RESULT($with_perl)

if test "$with_perl" = yes ; then
	CF_PROG_PERL(5.004)
	if test "$PERL" = no; then
	    AC_ERROR([perl not found])
	fi
	PERLLIB=`$PERL -MConfig -e 'print $Config{privlib}'`
	AC_DEFINE(OPT_PERL)
	EXTRAOBJS="$EXTRAOBJS perl\$o"
	BUILTSRCS="$BUILTSRCS perl.c"
	cf_prefix=`$PERL -MConfig -e 'print $Config{shrpenv}'`
	ac_link="$cf_prefix $ac_link"
	CF_CHECK_CFLAGS(`$PERL -MExtUtils::Embed -e ccopts`)
	LIBS="$LIBS `$PERL -MExtUtils::Embed -e ldopts`"
	EXTRA_INSTALL_DIRS="$EXTRA_INSTALL_DIRS \$(INSTALL_PERL_DIRS)"
	EXTRA_INSTALL_FILES="$EXTRA_INSTALL_FILES \$(INSTALL_PERL_FILES)"

    AC_TRY_LINK([
#include <EXTERN.h>
#include <perl.h>
#include <XSUB.h>
    ],[
    PerlInterpreter* interp = perl_alloc();
    perl_construct(interp);
    perl_parse(interp, (XSINIT_t)0, 0, (char **)0, (char **)0);
    Perl_croak(aTHX_ "Why:%s\n", "Bye!");
],,[AC_MSG_ERROR(Cannot link with Perl interpreter)])

fi
AC_SUBST(PERLLIB)
AC_SUBST(PERL)
AC_SUBST(EXTRA_INSTALL_DIRS)
AC_SUBST(EXTRA_INSTALL_FILES)
])

dnl CF_WITH_PKG_CONFIG_LIBDIR version: 14 updated: 2021/10/25 19:21:54
dnl -------------------------
dnl Allow the choice of the pkg-config library directory to be overridden.
dnl
dnl pkg-config uses a search-list built from these colon-separated lists of
dnl directories:
dnl a) $PKG_CONFIG_PATH (tested first, added if set)
dnl b) $PKG_CONFIG_LIBDIR (tested second, added if set)
dnl c) builtin-list (added if $PKG_CONFIG_LIBDIR is not set)
dnl
dnl pkgconf (used with some systems such as FreeBSD in place of pkg-config)
dnl optionally ignores $PKG_CONFIG_LIBDIR.
AC_DEFUN([CF_WITH_PKG_CONFIG_LIBDIR],[

case "$PKG_CONFIG" in
(no|none|yes)
	AC_MSG_CHECKING(for pkg-config library directory)
	;;
(*)
	AC_MSG_CHECKING(for $PKG_CONFIG library directory)
	;;
esac

cf_search_path=`echo "$PKG_CONFIG_LIBDIR" | sed -e 's/:/ /g' -e 's,^[[ 	]]*,,'`
AC_ARG_WITH(pkg-config-libdir,
	[  --with-pkg-config-libdir=XXX use given directory for installing pc-files],
	[cf_search_path=$withval],
	[test "x$PKG_CONFIG" != xnone && cf_search_path=yes])

case x$cf_search_path in
(x/*)
	;;
(xyes)
	cf_search_path=
	CF_VERBOSE(auto...)
	# Look for the library directory using the same prefix as the executable
	AC_MSG_CHECKING(for search-list)
	if test "x$PKG_CONFIG" = xnone
	then
		cf_path=$prefix
	else
		cf_path=`echo "$PKG_CONFIG" | sed -e 's,/[[^/]]*/[[^/]]*$,,'`

		# works for pkg-config since version 0.24 (2009)
		cf_pkg_program=`echo "$PKG_CONFIG" | sed -e 's,^.*/,,'`
		cf_search_path=`"$PKG_CONFIG" --variable=pc_path "$cf_pkg_program" | tr : ' '`

		# works for pkgconf since version 0.8.3 (2012)
		test -z "$cf_search_path" && \
		cf_search_path=`pkgconf --variable=pc_path pkgconf | tr : ' '`

		# works for pkg-config since import in 2005 of original 2001 HP code.
		test -z "$cf_search_path" && \
		cf_search_path=`
		"$PKG_CONFIG" --debug --exists no-such-package 2>&1 | awk "\
/^Scanning directory #[1-9][0-9]* '.*'$/{ \
	sub(\"^[[^']]*'\",\"\"); \
	sub(\"'.*\",\"\"); \
	printf \" %s\", \\[$]0; } \
/trying path:/{
	sub(\"^.* trying path: \",\"\");
	sub(\" for no-such-package.*$\",\"\");
	printf \" %s\", \\[$]0;
}
{ next; } \
"`
	fi

	if test -z "$cf_search_path"
	then
		# If you don't like using the default architecture, you have to specify
		# the intended library directory and corresponding compiler/linker
		# options.
		#
		# This case allows for Debian's 2014-flavor of multiarch, along with
		# the most common variations before that point.  Some other variants
		# spell the directory differently, e.g., "pkg-config", and put it in
		# unusual places.
		#
		# pkg-config has always been poorly standardized, which is ironic...
		case x`(arch) 2>/dev/null` in
		(*64)
			cf_test_path="\
				$cf_path/lib/*64-linux-gnu \
				$cf_path/share \
				$cf_path/lib64 \
				$cf_path/lib32 \
				$cf_path/lib"
			;;
		(*)
			cf_test_path="\
				$cf_path/lib/*-linux-gnu \
				$cf_path/share \
				$cf_path/lib32 \
				$cf_path/lib \
				$cf_path/libdata"
			;;
		esac
		for cf_config in $cf_test_path
		do
			test -d "$cf_config/pkgconfig" && cf_search_path="$cf_search_path $cf_config/pkgconfig"
		done
	fi

	AC_MSG_RESULT($cf_search_path)

	;;
(*)
	;;
esac

AC_MSG_CHECKING(for first directory)
cf_pkg_config_path=none
for cf_config in $cf_search_path
do
	if test -d "$cf_config"
	then
		cf_pkg_config_path=$cf_config
		break
	fi
done
AC_MSG_RESULT($cf_pkg_config_path)

if test "x$cf_pkg_config_path" != xnone ; then
	# limit this to the first directory found
	PKG_CONFIG_LIBDIR="$cf_pkg_config_path"
fi

if test -z "$PKG_CONFIG_LIBDIR" && test -n "$cf_search_path"
then
	AC_MSG_CHECKING(for workaround)
	if test "$prefix" = "NONE" ; then
		cf_prefix="$ac_default_prefix"
	else
		cf_prefix="$prefix"
	fi
	eval cf_libdir=$libdir
	cf_libdir=`echo "$cf_libdir" | sed -e "s,^NONE,$cf_prefix,"`
	cf_backup=
	for cf_config in $cf_search_path
	do
		case $cf_config in
		$cf_libdir/pkgconfig)
			PKG_CONFIG_LIBDIR=$cf_libdir/pkgconfig
			break
			;;
		*)
			test -z "$cf_backup" && cf_backup=$cf_config
			;;
		esac
	done
	test -z "$PKG_CONFIG_LIBDIR" && PKG_CONFIG_LIBDIR=$cf_backup
	AC_MSG_RESULT($PKG_CONFIG_LIBDIR)
fi

AC_SUBST(PKG_CONFIG_LIBDIR)
])dnl

dnl CF_SOCKS version: 7 updated: 2010/04/21 06:20:50
dnl --------
dnl Check for socks library
dnl $1 = the [optional] directory in which the library may be found
AC_DEFUN([CF_SOCKS],[
  CF_ADD_OPTIONAL_PATH($1, [socks library])
  CF_FIND_LINKAGE([
#include <stdio.h>
],[
      Raccept((char *)0)
],
      socks,
      cf_cv_have_socks=yes,
      cf_cv_have_socks=no)

  if test "$cf_cv_have_socks" = yes ; then
    AC_DEFINE(SOCKS)

    AC_DEFINE(accept,Raccept)
    AC_DEFINE(bind,Rbind)
    AC_DEFINE(connect,Rconnect)
    AC_DEFINE(getpeername,Rgetpeername)
    AC_DEFINE(getsockname,Rgetsockname)
    AC_DEFINE(listen,Rlisten)
    AC_DEFINE(recvfrom,Rrecvfrom)
    AC_DEFINE(select,Rselect)

    CF_ADD_INCDIR($cf_cv_header_path_socks)
    CF_ADD_LIBDIR($cf_cv_library_path_socks)
	LIBS="$cf_cv_library_file_socks $LIBS"
  else
    AC_MSG_ERROR(cannot link with socks library)
  fi
])dnl

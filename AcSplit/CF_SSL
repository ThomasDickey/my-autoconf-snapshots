dnl CF_SSL version: 8 updated: 2007/07/28 12:51:36
dnl ------
dnl Check for ssl library
dnl $1 = the [optional] directory in which the library may be found
AC_DEFUN([CF_SSL],[
  case "$1" in #(vi
  no) #(vi
      ;;
  yes) #(vi
      ;;
  *)
      CF_ADD_SEARCHPATH([$1], [AC_MSG_ERROR(cannot find ssl library under $1)])
      ;;
  esac

  CF_FIND_LINKAGE(CF__SSL_HEAD,
      CF__SSL_BODY,
      ssl,
      cf_cv_have_ssl=yes,
      cf_cv_have_ssl=no,
      openssl,
      [-lcrypto])

  if test "$cf_cv_have_ssl" = yes ; then
    if test -n "$cf_cv_header_path_ssl" ; then
      AC_DEFINE(USE_SSL)
      AC_DEFINE(USE_X509_SUPPORT)

      case $cf_cv_header_path_ssl in
      */openssl)
          AC_DEFINE(USE_OPENSSL_INCL)
          ;;
      esac

      case $cf_cv_header_path_ssl in
      /usr/include/openssl)
          ;;
      *)
          CF_ADD_CFLAGS([-I$cf_cv_header_path_ssl])
          ;;
      esac

    fi
    if test -n "$cf_cv_library_path_ssl" ; then
      LDFLAGS="-L$cf_cv_library_path_ssl $LDFLAGS"
    fi
    LIBS="-lssl -lcrypto $LIBS"
  fi
])dnl

dnl CF_BOOL_DECL version: 11 updated: 2025/02/22 20:49:45
dnl ------------
dnl Test if 'bool' is a builtin type in the configured C/C++ compiler.  Some
dnl older compilers (e.g., gcc 2.5.8) don't support 'bool' directly; gcc
dnl 2.6.3 does, in anticipation of the ANSI C++ standard.
dnl
dnl $1 is the shell variable to store the result in, if not $cv_cv_builtin_bool
AC_DEFUN([CF_BOOL_DECL],
[
AC_REQUIRE([CF_C99_STDBOOL_H])
AC_CACHE_CHECK(for builtin bool type, ifelse($1,,cf_cv_builtin_bool,[$1]),[
	AC_TRY_COMPILE([
$ac_includes_default
],[bool x = false; (void)x],
		[ifelse($1,,cf_cv_builtin_bool,[$1])=yes],
		[ifelse($1,,cf_cv_builtin_bool,[$1])=no])])
])dnl

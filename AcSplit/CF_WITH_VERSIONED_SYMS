dnl CF_WITH_VERSIONED_SYMS version: 1 updated: 2014/11/02 16:11:49
dnl ----------------------
dnl Use this when building shared library with ELF, to markup symbols with the
dnl version identifier from the given input file.  Generally that identifier is
dnl the same as the SONAME at which the symbol was first introduced.
dnl
dnl $1 = basename of the ".sym" file (default $PACKAGE)
AC_DEFUN([CF_WITH_VERSIONED_SYMS],
[
AC_MSG_CHECKING(if versioned-symbols file should be used)
AC_ARG_WITH(versioned-syms,
	[  --with-versioned-syms=X markup versioned symbols using ld],
	[with_versioned_syms=$withval],
	[with_versioned_syms=no])
if test "x$with_versioned_syms" = xyes
then
	with_versioned_syms='${srcdir}/package/ifelse($1,,${PACKAGE},[$1]).map'
	AC_SUBST(PACKAGE)
fi
AC_MSG_RESULT($with_versioned_syms)
if test "x$with_versioned_syms" != xno
then
	case "x$MK_SHARED_LIB" in #(vi
	*-Wl,*) #(vi
		MK_SHARED_LIB=`echo "$MK_SHARED_LIB" | sed -e "s%-Wl,%-Wl,--version-script,$with_versioned_syms -Wl,%"`
		;;
	*)
		;;
	esac
fi
])dnl

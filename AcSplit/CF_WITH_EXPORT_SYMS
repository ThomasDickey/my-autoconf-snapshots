dnl CF_WITH_EXPORT_SYMS version: 1 updated: 2014/11/02 16:11:49
dnl -------------------
dnl Use this with libtool to specify the list of symbols that may be exported.
dnl The input file contains one symbol per line; comments work with "#".
dnl
dnl $1 = basename of the ".sym" file (default $PACKAGE)
AC_DEFUN([CF_WITH_EXPORT_SYMS],
[
AC_MSG_CHECKING(if exported-symbols file should be used)
AC_ARG_WITH(export-syms,
	[  --with-export-syms=XXX  limit exported symbols using libtool],
	[with_export_syms=$withval],
	[with_export_syms=no])
if test "x$with_export_syms" = xyes
then
	with_export_syms='${srcdir}/package/ifelse($1,,${PACKAGE},[$1]).sym'
	AC_SUBST(PACKAGE)
fi
AC_MSG_RESULT($with_export_syms)
if test "x$with_export_syms" != xno
then
	LIBTOOL_OPTS="$LIBTOOL_OPTS -export-symbols $with_export_syms"
	AC_SUBST(LIBTOOL_OPTS)
fi
])dnl

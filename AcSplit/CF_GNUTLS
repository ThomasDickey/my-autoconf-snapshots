dnl CF_GNUTLS version: 10 updated: 2008/04/13 10:56:06
dnl ---------
dnl Check for gnutls library (TLS "is" SSL)
dnl $1 = the [optional] directory in which the library may be found
dnl $2 = the [optional] stub file to provide OpenSSL compatibility
AC_DEFUN([CF_GNUTLS],[
  CF_ADD_OPTIONAL_PATH($1, [ssl library])

  ifelse($2,,[AC_DEFINE(USE_GNUTLS_INCL)],[AC_DEFINE(USE_GNUTLS_FUNCS)])
  cf_gnutls_CPPFLAGS=$CPPFLAGS

  CF_FIND_LINKAGE(CF__SSL_HEAD,
      CF__SSL_BODY,
      gnutls,
      cf_cv_have_gnutls=yes,
      cf_cv_have_gnutls=no,
      ,
      ifelse($2,,[-lgnutls-openssl -lgnutls-extra -lgnutls -lcrypt],[-lgnutls -lcrypt]))

  CPPFLAGS=$cf_gnutls_CPPFLAGS

  if test "$cf_cv_have_gnutls" = yes ; then
    if test -n "$cf_cv_header_path_gnutls" ; then
      AC_DEFINE(USE_SSL)
      case $cf_cv_header_path_gnutls in
      /usr/include/gnutls)
          ;;
      *)
          CF_ADD_INCDIR($cf_cv_header_path_gnutls)
          ;;
      esac
    fi
    if test -n "$cf_cv_library_path_gnutls" ; then
      CF_ADD_LIBDIR($cf_cv_library_path_gnutls)
    fi
    LIBS="-lgnutls -lcrypt $LIBS"
  fi

  ifelse($2,,[
  AC_CHECK_LIB(gnutls-openssl,SSL_connect,
      [LIBS="-lgnutls-openssl $LIBS"],
      [AC_CHECK_LIB(gnutls-extra,SSL_connect,
          [LIBS="-lgnutls-extra $LIBS"],
          [AC_MSG_ERROR(cannot find gnutls openssl functions)])])],[EXTRA_OBJS="$EXTRA_OBJS $2"])

  CF_CHECK_SSL_X509
])dnl

dnl Checks for libraries.  At least one UNIX system, Apple Macintosh
dnl Rhapsody 5.5, does not have -lm.  We cannot use the simpler
dnl AC_CHECK_LIB(m,sin), because that fails for C++.
AC_DEFUN([CF_MATH_LIB],
[
AC_CACHE_CHECK(if -lm needed for math functions,
	cf_cv_need_libm,[
	AC_TRY_LINK([
	#include <stdio.h>
	#include <math.h>
	],
	[double x = rand(); printf("sin(1.0) = %g\n", sin(x));],
	[cf_cv_need_libm=no],
	[cf_cv_need_libm=yes])])
ifelse($1,,[
if test "$cf_cv_need_libm" = yes
then
	LIBS="$LIBS -lm"
fi
],[$1=-lm])
])

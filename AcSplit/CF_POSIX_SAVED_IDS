dnl CF_POSIX_SAVED_IDS version: 4 updated: 2006/06/18 19:38:48
dnl ------------------
dnl
dnl Check first if saved-ids are always supported.  Some systems
dnl such require runtime checks.
AC_DEFUN([CF_POSIX_SAVED_IDS],
[
AC_CACHE_CHECK(if POSIX saved-ids are supported,cf_cv_posix_saved_ids,[
AC_TRY_LINK(
[
#include <unistd.h>
],[
#if defined(_POSIX_SAVED_IDS) && (_POSIX_SAVED_IDS > 0)
int x = seteuid(geteuid());
#else
make an error
#endif
],[cf_cv_posix_saved_ids=yes
],[
AC_TRY_RUN([
#ifdef HAVE_STDLIB_H
#include <stdlib.h>
#endif
#include <unistd.h>
int main()
{
	long code = sysconf(_SC_SAVED_IDS);
	exit ((code > 0) ? 0 : 1);
}],
	cf_cv_posix_saved_ids=yes,
	cf_cv_posix_saved_ids=no,
	cf_cv_posix_saved_ids=unknown)
])
])

test "$cf_cv_posix_saved_ids" = yes && AC_DEFINE(HAVE_POSIX_SAVED_IDS)
])

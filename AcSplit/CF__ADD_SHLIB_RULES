dnl CF__ADD_SHLIB_RULES version: 1 updated: 2013/06/15 16:55:42
dnl -------------------
dnl Append rules for creating, installing, uninstalling and cleaning library.
dnl In particular, this is needed for shared libraries since there are symbolic
dnl links which depend on configuration choices.
dnl
dnl The logic is controlled by these cache variables:
dnl $cf_cv_do_symlinks
dnl $cf_cv_shlib_version
dnl
dnl $1 = makefile to append to
dnl $2 = model (normal, shared, libtool)
dnl $3 = pathname of library file in build-tree, used as makefile target
dnl $4 = objects (dependencies)
dnl $5 = additional libraries needed to link the shared library
define([CF__ADD_SHLIB_RULES],[

cf_dirname=`echo "$3" | sed -e 's,/[[^/]]*$,,'`
test "x$3" = "x$cf_dirname" && cf_dirname=.

cf_libname=`basename $3`
cf_liblink=$cf_libname
cf_libroot=$cf_libname

if test "x$cf_cv_do_symlinks" != xno
then
	case "x$cf_cv_shlib_version" in #(vi
	xrel) #(vi
		cf_liblink="$cf_libroot.\${ABI_VERSION}"
		cf_libname="$cf_libroot.\${REL_VERSION}"
		;;
	xabi)
		cf_liblink="$cf_libroot.\${REL_VERSION}"
		cf_libname="$cf_libroot.\${ABI_VERSION}"
		;;
	esac
fi

cat >>$1 <<CF_EOF

# generated by CF__ADD_SHLIB_RULES
# symlinks: $cf_cv_do_symlinks
# shlibver: $cf_cv_shlib_version

CF_EOF

if test "x$cf_dirname" != "x."
then
cat >>$1 <<CF_EOF
$cf_dirname : mkdir -p \$@
CF_EOF
fi

cat >>$1 <<CF_EOF
$cf_libname :: \\
CF_EOF

if test "x$cf_dirname" != "x."
then
cat >>$1 <<CF_EOF
		$cf_dirname \\
CF_EOF
fi

cat >>$1 <<CF_EOF
		\$(SHARED_OBJS)
	@echo linking \[$]@
	\$(LIBTOOL_CREATE) $4 ifelse($5,,,[$5]) \$(LDFLAGS)
CF_EOF

if test x$cf_cv_do_symlinks != xno
then
cat >>$1 <<CF_EOF
	cd $cf_dirname && (\$(LN_S) $cf_libname $cf_liblink; \$(LN_S) $cf_liblink $cf_libroot; )
CF_EOF
fi

cat >>$1 <<CF_EOF

install \\
install.libs :: \$(DESTDIR)\$(libdir)/$cf_libname

\$(DESTDIR)\$(libdir)/$cf_libname :: \\
		\$(DESTDIR)\$(libdir) \\
		\$(SHARED_OBJS)
	@echo linking \[$]@
	\$(LIBTOOL_CREATE) \$(SHARED_OBJS) \$(SHLIB_LIST) \$(LDFLAGS)
CF_EOF

if test x$cf_cv_do_symlinks != xno
then
cat >>$1 <<CF_EOF
	cd \$(DESTDIR)\$(libdir) && (\$(LN_S) $cf_libname $cf_liblink; \$(LN_S) $cf_liblink $cf_libroot; )
CF_EOF
fi

if test x$2 = xshared
then
cat >>$1 <<CF_EOF
	- test -z "\$(DESTDIR)" && /sbin/ldconfig
CF_EOF
fi

cat >>$1 <<CF_EOF

uninstall \\
uninstall.libs ::
	@echo uninstalling \$(DESTDIR)\$(libdir)/$cf_libname
CF_EOF

if test x$cf_cv_do_symlinks != xno
then
cat >>$1 <<CF_EOF
	-rm -f \$(DESTDIR)\$(libdir)/$cf_libroot
	-rm -f \$(DESTDIR)\$(libdir)/$cf_liblink
CF_EOF
fi

cat >>$1 <<CF_EOF
	-rm -f \$(DESTDIR)\$(libdir)/$cf_libname

clean \\
clean.libs ::
CF_EOF

if test x$cf_cv_do_symlinks != xno
then
cat >>$1 <<CF_EOF
	-rm -f $cf_dirname/$cf_libroot
	-rm -f $cf_dirname/$cf_liblink
CF_EOF
fi

cat >>$1 <<CF_EOF
	-rm -f $cf_dirname/$cf_libname

mostlyclean::
	-rm -f \$(SHARED_OBJS)
# end generated by CF__ADD_SHLIB_RULES
CF_EOF
])dnl

dnl CF_DEFINE_PATHNAME version: 3 updated: 1997/12/17 23:35:33
dnl ------------------
dnl Expand a pathname into a #define.  It may contain symbols that aren't
dnl fully expanded, since autoconf puts off substituting --prefix, etc., until
dnl the end.
dnl $1 = message
dnl $2 = symbol to define
dnl $3 = expression to evaluate
AC_DEFUN([CF_DEFINE_PATHNAME],[

eval '$2='$3
eval 'cf_prefix='$prefix

case "$cf_prefix" in
NONE)	#(vi
	cf_prefix=`echo $cf_prefix | sed -e s@'^NONE@'$ac_default_prefix@`
	;;
NONE/*)	#(vi
	cf_prefix=`echo $cf_prefix | sed -e s@'^NONE/'@$ac_default_prefix/@`
	;;
esac
$2=`echo $$2 | sed -e s@'^NONE/'@$cf_prefix/@`

AC_MSG_CHECKING($1)
AC_DEFINE_UNQUOTED($2,"[$]$2")
AC_MSG_RESULT([$]$2)

])dnl

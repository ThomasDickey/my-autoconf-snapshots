dnl CF_GNAT_PROJECTS version: 5 updated: 2014/05/24 13:30:20
dnl ----------------
dnl GNAT projects are configured with ".gpr" project files.
dnl GNAT libraries are a further development, using the project feature.
AC_DEFUN([CF_GNAT_PROJECTS],
[
AC_REQUIRE([CF_GNAT_VERSION])

cf_gnat_projects=no

AC_MSG_CHECKING(if GNAT supports project files)
case $cf_gnat_version in #(vi
3.[[0-9]]*) #(vi
	;;
*)
	case $cf_cv_system_name in #(vi
	cygwin*|msys*) #(vi
		;;
	*)
		mkdir conftest
		cd conftest
		mkdir lib obj
		cat >>library.gpr <<CF_EOF
project Library is
  for Library_Name use "ConfTest";
  for Object_Dir use "obj";
  for Library_Version use "libConfTest.so.1";
  for Library_Kind use "dynamic";
  for Library_Dir use "lib";
  for Source_Dirs use (".");
end Library;
CF_EOF
		cat >>confpackage.ads <<CF_EOF
package ConfPackage is
   procedure conftest;
end ConfPackage;
CF_EOF
		cat >>confpackage.adb <<CF_EOF
with Text_IO;
package body ConfPackage is
   procedure conftest is
   begin
      Text_IO.Put ("Hello World");
      Text_IO.New_Line;
   end conftest;
end ConfPackage;
CF_EOF
		if ( $cf_ada_make -Plibrary.gpr 1>&AC_FD_CC 2>&1 ); then
			cf_gnat_projects=yes
		fi
		cd ..
		rm -rf conftest
		;;
	esac
	;;
esac
AC_MSG_RESULT($cf_gnat_projects)
])dnl

dnl CF_WITH_LIB_BASENAME version: 2 updated: 2023/11/22 20:48:30
dnl --------------------
dnl Allow for overriding the basename of a library, i.e., the part to which
dnl prefixes/suffixes are attached.
dnl
dnl $1 = variable to set
dnl $2 = option name
dnl $3 = default basename for library, if omitted use $2
AC_DEFUN([CF_WITH_LIB_BASENAME],
[
AC_MSG_CHECKING(for desired basename for $2 library)
AC_ARG_WITH($2-libname,
	[[  --with-$2-libname[=XXX] override ifelse($3,,$2,$3) basename of library]],
	[with_lib_basename=$withval],
	[with_lib_basename=ifelse($3,,$2,$3)])
$1="$with_lib_basename"

case "x[$]$1" in
(x|xno|xnone|xyes)
	$1=ifelse($3,,$2,$3)
	;;
(*)
	;;
esac

AC_MSG_RESULT([$]$1)
AC_SUBST($1)
])dnl

dnl CF_MAKE_AR_RULES version: 6 updated: 2019/12/17 20:34:13
dnl ----------------
dnl Check if the 'make' program knows how to interpret archive rules.  Though
dnl this is common practice since the mid-80's, there were some holdouts noted
dnl in 1997 among the BSD-based Unix systems.  Twenty years later, the BSDs
dnl still fail to provide this POSIX feature.
AC_DEFUN([CF_MAKE_AR_RULES],
[
AC_MSG_CHECKING(if ${MAKE:-make} program knows about archives)
AC_CACHE_VAL(cf_cv_ar_rules,[
cf_dir=subd$$
cf_cv_ar_rules=unknown
mkdir $cf_dir
cat >$cf_dir/makefile <<CF_EOF
SHELL = /bin/sh
AR = ar crv
CC = $CC

.SUFFIXES:
.SUFFIXES: .c .o .a

all:  conf.a

.c.a:
	\$(CC) -c $<
	\$(AR) \$[]@ \$[]*.o
conf.a : conf.a(conftest.o)
CF_EOF
cat >$cf_dir/conftest.c <<CF_EOF
#include <stdio.h>
extern int conftest(char *name);
int conftest(char *name)
{
	FILE *fp = fopen(name, "r");
	int rc = 0;
	if (fp != 0) {
		fclose(fp);
		rc = 1;
	}
	return rc;
}
CF_EOF
CDPATH=; export CDPATH
if ( cd $cf_dir && ${MAKE:-make} 2>&AC_FD_CC >&AC_FD_CC && test -f conf.a )
then
	cf_cv_ar_rules=yes
else
	echo ... library-rule did not create an archive >&AC_FD_CC
	rm -f $cf_dir/conftest.o
	cat >$cf_dir/makefile <<CF_EOF
SHELL = /bin/sh
AR = ar crv
CC = $CC

.SUFFIXES:
.SUFFIXES: .c .o

all:  conf.a

.c.o:
	\$(CC) -c $<

conf.a : conftest.o
	\$(AR) \$[]@ \$?
CF_EOF
	CDPATH=; export CDPATH
	if ( cd $cf_dir && ${MAKE:-make} 2>&AC_FD_CC >&AC_FD_CC && test -f conf.a )
	then
		cf_cv_ar_rules=no
	else
		AC_MSG_ERROR(I do not know how to construct a library)
	fi
fi
rm -rf $cf_dir
])
AC_MSG_RESULT($cf_cv_ar_rules)
])dnl

dnl CF_ADD_SEARCHPATH version: 2 updated: 2007/07/26 17:35:47
dnl -----------------
dnl Set $CPPFLAGS and $LDFLAGS with the directories given via the parameter.
dnl They can be either the common root of include- and lib-directories, or the
dnl lib-directory (to allow for things like lib64 directories).
dnl See also CF_FIND_LINKAGE.
dnl
dnl $1 is the list of colon-separated directory names to search.
dnl $2 is the action to take if a parameter does not yield a directory.
AC_DEFUN([CF_ADD_SEARCHPATH],
[
for cf_searchpath in `echo "$1" | tr : ' '`; do
	if test -d $cf_searchpath/include; then
		CPPFLAGS="$CPPFLAGS -I$cf_searchpath/include"
	elif test -d $cf_searchpath/../include ; then
		CPPFLAGS="$CPPFLAGS -I$cf_searchpath/../include"
	ifelse([$2],,,[else
$2])
	fi
	if test -d $cf_searchpath/lib; then
		LDFLAGS="$LDFLAGS -L$cf_searchpath/lib"
	elif test -d $cf_searchpath ; then
		LDFLAGS="$LDFLAGS -L$cf_searchpath"
	ifelse([$2],,,[else
$2])
	fi
done
])

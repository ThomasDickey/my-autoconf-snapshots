dnl CF_X_FREETYPE version: 8 updated: 2003/12/30 11:30:39
dnl -------------
dnl Check for X FreeType headers and libraries (XFree86 4.x).
AC_DEFUN([CF_X_FREETYPE],
[
AC_CACHE_CHECK(for X FreeType headers,cf_cv_x_freetype_incs,[
	cf_cv_x_freetype_incs="`freetype-config --cflags 2>/dev/null`"
])

AC_CACHE_CHECK(for X FreeType libraries,cf_cv_x_freetype_libs,[

cf_save_LIBS="$LIBS"
cf_save_INCS="$CPPFLAGS"

cf_cv_x_freetype_libs="-lXft `freetype-config --libs 2>/dev/null`"

LIBS="$cf_cv_x_freetype_libs $LIBS"
CPPFLAGS="$cf_cv_x_freetype_incs $CPPFLAGS"

AC_TRY_LINK([
#include <X11/Xlib.h>
#include <X11/extensions/Xrender.h>
#include <X11/Xft/Xft.h>],[
	XftPattern  *pat = XftNameParse ("name");
	],[],[cf_cv_x_freetype_libs=])
	LIBS="$cf_save_LIBS"
	CPPFLAGS="$cf_save_INCS"
])

if test -n "$cf_cv_x_freetype_libs" ; then
	LIBS="$cf_cv_x_freetype_libs $LIBS"
	CPPFLAGS="$cf_cv_x_freetype_incs $CPPFLAGS"
	AC_DEFINE(XRENDERFONT)
else
	CPPFLAGS=`echo "$CPPFLAGS" | sed -e s/-DXRENDERFONT//`
fi

# FIXME: revisit this if needed
AC_SUBST(XRENDERFONT)
AC_SUBST(HAVE_TYPE_FCCHAR32)
AC_SUBST(HAVE_TYPE_XFTCHARSPEC)
])

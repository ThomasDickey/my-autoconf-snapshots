dnl CF_X_FREETYPE version: 15 updated: 2006/02/12 17:30:04
dnl -------------
dnl Check for X FreeType headers and libraries (XFree86 4.x, etc).
dnl
dnl First check for the appropriate config program, since the developers for
dnl these libraries change their configuration (and config program) more or
dnl less randomly.  If we cannot find the config program, do not bother trying
dnl to guess the latest variation of include/lib directories.
dnl
dnl If either or both of these configure-script options are not given, rely on
dnl the output of the config program to provide the dnl cflags/libs options:
dnl	--with-freetype-cflags
dnl	--with-freetype-libs
AC_DEFUN([CF_X_FREETYPE],
[
cf_extra_freetype_libs=
FREETYPE_CONFIG=
FREETYPE_PARAMS=

AC_PATH_PROG(FREETYPE_PKG_CONFIG, pkg-config, none)
if test "$FREETYPE_PKG_CONFIG" != none && "$FREETYPE_PKG_CONFIG" --exists xft; then
	FREETYPE_CONFIG=$FREETYPE_PKG_CONFIG
	FREETYPE_PARAMS=xft
else
	AC_PATH_PROG(FREETYPE_XFT_CONFIG, xft-config, none)
	if test "$FREETYPE_XFT_CONFIG" != none; then
		FREETYPE_CONFIG=$FREETYPE_XFT_CONFIG
	else
		cf_extra_freetype_libs="-lXft"
		AC_PATH_PROG(FREETYPE_OLD_CONFIG, freetype-config, none)
		if test "$FREETYPE_OLD_CONFIG" != none; then
			FREETYPE_CONFIG=$FREETYPE_OLD_CONFIG
		fi
	fi
fi

if test -n "$FREETYPE_CONFIG" ; then
withval=
AC_ARG_WITH(freetype-cflags,
	[  --with-freetype-cflags  -D/-I options for compiling with FreeType],
[cf_cv_x_freetype_incs="$withval"
 CF_VERBOSE(freetype-cflags $cf_cv_x_freetype_incs)
],[
AC_CACHE_CHECK(for X FreeType headers,cf_cv_x_freetype_incs,[
	cf_cv_x_freetype_incs="`$FREETYPE_CONFIG $FREETYPE_PARAMS --cflags 2>/dev/null`"
])])
withval=
AC_ARG_WITH(freetype-libs,
	[  --with-freetype-libs    -L/-l options to link FreeType],
[cf_cv_x_freetype_libs="$withval"
 CF_VERBOSE(freetype-libs $cf_cv_x_freetype_libs)
],[
AC_CACHE_CHECK(for X FreeType libraries,cf_cv_x_freetype_libs,[

cf_save_LIBS="$LIBS"
cf_save_INCS="$CPPFLAGS"

cf_cv_x_freetype_libs="$cf_extra_freetype_libs `$FREETYPE_CONFIG $FREETYPE_PARAMS --libs 2>/dev/null`"

LIBS="$cf_cv_x_freetype_libs $LIBS"
CPPFLAGS="$cf_cv_x_freetype_incs $CPPFLAGS"

AC_TRY_LINK([
#include <X11/Xlib.h>
#include <X11/extensions/Xrender.h>
#include <X11/Xft/Xft.h>],[
	XftPattern  *pat = XftNameParse ("name");
	],[],[cf_cv_x_freetype_libs=])
	LIBS="$cf_save_LIBS"
	CPPFLAGS="$cf_save_INCS"
])])

if test -n "$cf_cv_x_freetype_libs" ; then
	LIBS="$cf_cv_x_freetype_libs $LIBS"
	CF_ADD_CFLAGS($cf_cv_x_freetype_incs)
	AC_DEFINE(XRENDERFONT)
else
	AC_MSG_WARN(No libraries found for FreeType)
	CPPFLAGS=`echo "$CPPFLAGS" | sed -e s/-DXRENDERFONT//`
fi

else
	AC_MSG_WARN(Cannot find FreeType configuration program)
	CPPFLAGS=`echo "$CPPFLAGS" | sed -e s/-DXRENDERFONT//`
fi

# FIXME: revisit this if needed
AC_SUBST(XRENDERFONT)
AC_SUBST(HAVE_TYPE_FCCHAR32)
AC_SUBST(HAVE_TYPE_XFTCHARSPEC)
])

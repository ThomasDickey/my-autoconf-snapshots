dnl CF_X_FREETYPE version: 7 updated: 2003/12/29 18:39:35
dnl -------------
dnl Check for X freetype libraries (XFree86 4.x).
dnl
dnl FIXME: -lfontconfig may be needed.
AC_DEFUN([CF_X_FREETYPE],
[
cf_freetype_libs="-lXft -lfreetype -lXrender"
AC_CACHE_CHECK(for X FreeType libraries,cf_cv_x_freetype,[

cf_save_LIBS="$LIBS"
LIBS="$cf_freetype_libs $LIBS"

AC_TRY_LINK([
#include <X11/Xlib.h>
#include <X11/extensions/Xrender.h>
#include <X11/Xft/Xft.h>],[
	XftPattern  *pat = XftNameParse ("name");
	],[cf_cv_x_freetype=yes],[cf_cv_x_freetype=no])
	LIBS="$cf_save_LIBS"
])
if test "$cf_cv_x_freetype" = yes ; then
	LIBS="$cf_freetype_libs $LIBS"
	AC_DEFINE(XRENDERFONT)
else
	CPPFLAGS=`echo "$CPPFLAGS" | sed -e s/-DXRENDERFONT//`
fi

# FIXME: revisit this if needed
AC_SUBST(XRENDERFONT)
AC_SUBST(HAVE_TYPE_FCCHAR32)
AC_SUBST(HAVE_TYPE_XFTCHARSPEC)
])
